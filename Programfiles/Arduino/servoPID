#include <Arduino.h>
#include <ESP32Servo.h>
 
// -------------------- Pins --------------------
static const int servoPanPin = 18;
static const int servoTiltPin = 19;
 
#define ENC_PAN_A 32
#define ENC_PAN_B 33
 
#define ENC_TILT_A 34
#define ENC_TILT_B 35
 
#define PPR 4096
 
// -------------------- Globals --------------------
Servo servoPan;
Servo servoTilt;
 
volatile long encoderCountPan = 0;
volatile long encoderCountTilt = 0;
 
volatile uint8_t lastEncodedPan = 0;
volatile uint8_t lastEncodedTilt = 0;
 
float maxSpeed = 50.0; // grader per sekund ved 100% pådrag
 
float servoPanPos = 90.0;
int servoPanMin = 5;
int servoPanMax = 165;
 
float servoTiltPos = 56.0;
int servoTiltMin = 20;
int servoTiltMax = 73;
 
 
float camera_width = 1280.0; // Kamerabredde i pixel
float camera_height = 720.0; // Kamerahøyde i pixel
float camera_H_fov = 110.0; // Kamerabredde i grader (horisontal FOV)
float camera_V_fov = 95.0; // Kamerahøyde i grader (vertical FOV)
 
float SP_pan;
float SP_tilt;
float e_pan = 0;
float e_tilt = 0;
 
unsigned long lastTime = 0;
 
// -------------------- Encoder Interrupts --------------------
void IRAM_ATTR updateEncoderPan() {
  bool MSB = digitalRead(ENC_PAN_A);
  bool LSB = digitalRead(ENC_PAN_B);
 
  uint8_t encoded = (MSB << 1) | LSB;
  uint8_t sum = (lastEncodedPan << 2) | encoded;
 
  if (sum == 0b0001 || sum == 0b0111 || sum == 0b1110 || sum == 0b1000) encoderCountPan++;
  if (sum == 0b0010 || sum == 0b0100 || sum == 0b1101 || sum == 0b1011) encoderCountPan--;
 
  lastEncodedPan = encoded;
}
 
void IRAM_ATTR updateEncoderTilt() {
  bool MSB = digitalRead(ENC_TILT_A);
  bool LSB = digitalRead(ENC_TILT_B);
 
  uint8_t encoded = (MSB << 1) | LSB;
  uint8_t sum = (lastEncodedTilt << 2) | encoded;
 
  if (sum == 0b0001 || sum == 0b0111 || sum == 0b1110 || sum == 0b1000) encoderCountTilt++;
  if (sum == 0b0010 || sum == 0b0100 || sum == 0b1101 || sum == 0b1011) encoderCountTilt--;
 
  lastEncodedTilt = encoded;
}
 
// -------------------- PID-klasse --------------------
class PIController {
    public:
      PIController(float Kp, float Ki) {
        this->Kp = Kp;
        this->Ki = Ki;
        this->integral = 0;
      }
 
      float compute(float error, float dt) {
        if (abs(error) < 10) {
          return 0;
        }
        float P_out = Kp * error;
        float I_out = Ki * integral;
 
        float output = P_out + I_out;
 
        // Metning av ∆x for maksimal hastighet
        float maxDelta = maxSpeed * dt;
        output = constrain(output, -maxDelta, maxDelta);
 
        // Clamping anti-windup: Bare integrer hvis ikke mettet
        float unclamped_output = P_out + I_out;
        if (output == unclamped_output) {
          integral += error * dt;
          integral = constrain(integral, -maxIntegral, maxIntegral);
        }
 
        return output;
      }
 
      void setMaxIntegral(float maxInt) {
        maxIntegral = maxInt;
      }
 
    private:
      float Kp, Ki;
      float integral;
      float maxIntegral = 100.0;
  };
 
PIController pid_pan(0.2, 0.1);
PIController pid_tilt(0.2, 0.1);
 
// -------------------- Setup --------------------
void setup() {
  Serial.begin(115200);
 
  servoPan.attach(servoPanPin);
  servoTilt.attach(servoTiltPin);
 
  // Setter servoene til minstesstilling for å initialisere encodere sin nullpunkt
  servoPan.write(servoPanMin);
  servoTilt.write(servoTiltMin);
  delay(1500); // Vent litt for å la servoene bevege seg til min posisjon før encodere initialiseres
 
  pinMode(ENC_PAN_A, INPUT);
  pinMode(ENC_PAN_B, INPUT);
 
  bool A_pan = digitalRead(ENC_PAN_A);
  bool B_pan = digitalRead(ENC_PAN_B);
  lastEncodedPan = (A_pan << 1) | B_pan;
 
  attachInterrupt(digitalPinToInterrupt(ENC_PAN_A), updateEncoderPan, CHANGE);
  attachInterrupt(digitalPinToInterrupt(ENC_PAN_B), updateEncoderPan, CHANGE);
 
  pinMode(ENC_TILT_A, INPUT);
  pinMode(ENC_TILT_B, INPUT);
 
  bool A_tilt = digitalRead(ENC_TILT_A);
  bool B_tilt = digitalRead(ENC_TILT_B);
  lastEncodedTilt = (A_tilt << 1) | B_tilt;
 
  attachInterrupt(digitalPinToInterrupt(ENC_TILT_A), updateEncoderTilt, CHANGE);
  attachInterrupt(digitalPinToInterrupt(ENC_TILT_B), updateEncoderTilt, CHANGE);
 
 
  // Setter servoene til midtsilling
  servoPan.write(servoPanPos);
  servoTilt.write(servoTiltPos);
  delay(3000);
 
}
 
// -------------------- Loop --------------------
void loop() {
  unsigned long now = millis();
  float dt = (now - lastTime) / 1000.0;
  lastTime = now;
 
  long count_pan, count_tilt;
  noInterrupts(); // Henter ut prosessverdi: y
  count_pan = encoderCountPan;
  count_tilt = encoderCountTilt;
  interrupts();
 
  // konverterer pulsene til grader. Inverterer siden encoder er monter speilet på samme akse
  float PV_pan = -(360.0 * count_pan) / PPR;
  float PV_tilt = -(360.0 * count_tilt) / PPR;
 
  // Henter innkommende data fra Serial Monitor
  if (Serial.available()) {
    String input = Serial.readStringUntil('\n');
    input.trim();
 
    int commaIndex = input.indexOf(',');
    if (commaIndex > 0) {
      String pan = input.substring(0, commaIndex); // Henter ut pan-verdien
      String tilt = input.substring(commaIndex + 1); // Henter ut tilt-verdien
 
      int panDeltaPixel = pan.toInt();
      int tiltDeltaPixel = tilt.toInt();
 
      float panDeltaAngle = map(panDeltaPixel, 0, camera_width, camera_H_fov / 2, -camera_H_fov / 2);
      float tiltDeltaAngle = map(tiltDeltaPixel, 0, camera_height, -camera_V_fov / 2, camera_V_fov / 2);
 
      e_pan = panDeltaAngle;
      e_tilt = tiltDeltaAngle;
     
    }
  }
 
  float delta_pan = pid_pan.compute(e_pan, dt);
  float delta_tilt = pid_tilt.compute(e_tilt, dt);
 
  servoPanPos += delta_pan;
  servoTiltPos += delta_tilt;
 
  servoPanPos = constrain(servoPanPos, servoPanMin, servoPanMax);
  //servoPanPos = map(servoPanPos, servoPanMin, servoPanMax, servoPanMax, servoPanMin);
  servoTiltPos = constrain(servoTiltPos, servoTiltMin, servoTiltMax);
 
  servoPan.write(servoPanPos);
  servoTilt.write(servoTiltPos);
 
  delay(50); // 20 Hz kontrollfrekvens
}
